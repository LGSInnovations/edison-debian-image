#!/usr/bin/perl -w

use strict;
use feature "switch";

my $lib_dir = "/usr/lib/emacsen-common";
my $invoked_by_old_pkg;
my $context;
my $flavor;

require $lib_dir . "/lib.pl";

umask 0022 or die "emacs-install: can't set umask, aborting.";

sub usage
{
  my($file_handle) = @_;
  if($invoked_by_old_pkg)
  {
    print $file_handle "Usage: emacs-install FLAVOR\n";
  }
  else
  {
    print $file_handle "Usage: emacs-install (--preinst|--postinst) FLAVOR\n";
  }
}

if(scalar(@ARGV) == 1)
{
  $invoked_by_old_pkg = 1;
  $flavor = $ARGV[0];
  $context = 'postinst;'
}
elsif (scalar(@ARGV) == 2)
{
  given($ARGV[0])
  {
    when('--preinst') { $context = 'preinst'; }
    when('--postinst') { $context = 'postinst'; }
    default
    {
      usage(*STDERR{IO});
      exit(1);
    }
  }
  $flavor = $ARGV[1];
}
else
{
  usage(*STDERR{IO});
  exit(1);
}

if($context eq 'preinst')
{
  unlink("$::installed_flavor_state_dir/$flavor");
  exit(0);
}

# Must be --postinst.

my @ordered_pkg_list =
    generate_add_on_install_list(get_installed_add_on_packages());

if(!$invoked_by_old_pkg)
{
  foreach my $pkg (@ordered_pkg_list)
  {
    print "Install $pkg for $flavor\n";
    my $script = $lib_dir . "/packages/install/$pkg";
    if(-e $script && (system($script, $flavor) != 0))
    {
      print STDERR "ERROR: install script from $pkg package failed\n";
      exit(1);
    }
    ex('touch', "$::installed_package_state_dir/$pkg");
  }
}
else # $invoked_by_old_pkg
{
  my @installed_flavors = get_installed_flavors();
  foreach my $pkg (@ordered_pkg_list)
  {
    print "Install $pkg for $flavor\n";
    my $script = $lib_dir . "/packages/install/$pkg";
    if(-e $script && (system($script, $flavor, @installed_flavors) != 0))
    {
      print STDERR "ERROR: install script from $pkg package failed\n";
      exit(1);
    }
    ex('touch', "$::installed_package_state_dir/$pkg");
  }
}

ex('touch', "$::installed_flavor_state_dir/$flavor");
